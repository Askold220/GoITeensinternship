//@version=4
study(title="(10. OB) Order Blocks (5)", overlay=true,max_bars_back=1000)

// Input options
range = input(title="Candle Range", type=input.integer,defval=15, minval=5, maxval=100, group="BASIC SETTINGS")

showBearishBOS = input(title="Show Bearish BOS Lines", type=input.bool,defval=true, group="Extras")
showBullishBOS = input(title="Show Bullish BOS Lines", type=input.bool,defval=true, group="Extras")

bearishOBColour = color.new(color.red, 90)
bullishOBColour = color.new(color.green, 90)
BOSCandleColour = color.new(color.yellow, 90)
bullishTrendColor = color.new(color.lime, 90)
bearishTrendColour= color.new(color.red, 90)

var int CandleColourMode = 0
var bool BosCandle = false

var int lastDownIndex=0
var float lastDown=0
var float lastLow=0

var int lastUpIndex=0
var float lastUp=0
var float lastUpLow=0
var float lastUpOpen=0
var float lastHigh=0
var float lastBullBreakLow=0

var int structureLowIndex=0
float structureLow=1000000

var longBoxes = array.new_box()
var shortBoxes = array.new_box()
var bosLines = array.new_line()

var int lastLongIndex=0
var int lastShortIndex=0
BosCandle:=false

structureLowIndexPointer(len) =>
    float minValue = highest(high, range)[1]
    int minIndex = bar_index
    for i = 1 to len
        if low[i] < minValue
            minValue := low[i]
            minIndex := bar_index[i]
    minIndex

structureLow:=lowest(low, range)[1]
structureLowIndex:=structureLowIndexPointer(range)

if(crossunder(low,structureLow))
    if((bar_index - lastUpIndex) < 1000)
        array.push(shortBoxes,box.new(left=lastUpIndex, top=lastHigh, bottom=lastUpLow,right=lastUpIndex, bgcolor=bearishOBColour,border_color=bearishOBColour, extend=extend.right))
        if(showBearishBOS)
            array.push(bosLines, line.new(structureLowIndex, structureLow, bar_index, structureLow, color= color.red, style=line.style_solid, width = 2))
        BosCandle:=true
        CandleColourMode:=0
        lastShortIndex:=lastUpIndex

if((array.size(shortBoxes)>0))
    for i = (array.size(shortBoxes)-1) to 0
        box=array.get(shortBoxes,i)
        top=box.get_top(box)
        left=box.get_left(box)
        if(close>top)
            box.delete(box)
            array.remove(shortBoxes, i)
            if((bar_index - lastDownIndex) < 1000 and bar_index>lastLongIndex)
                array.push(longBoxes, box.new(left=lastDownIndex, top=lastDown, bottom=lastLow,right=lastDownIndex, bgcolor=bullishOBColour,border_color=bullishOBColour, extend=extend.right))
                if(showBullishBOS)
                    array.push(bosLines, line.new(left, top, bar_index, top, color= color.green, style=line.style_solid, width = 1))
                BosCandle:=true
                CandleColourMode:=1
                lastLongIndex:=bar_index
                lastBullBreakLow:=low

if((array.size(longBoxes) > 0))
    for i = (array.size(longBoxes)-1) to 0
        lbox=array.get(longBoxes,i)
        bottom=box.get_bottom(lbox)
        top=box.get_top(lbox)
        if(close<bottom)
            array.remove(longBoxes, i)
            box.delete(lbox)

CandleColour= CandleColourMode==1?bullishTrendColor:bearishTrendColour
CandleColour:= BosCandle?BOSCandleColour:CandleColour
barcolor(CandleColour)

if(close<open)
    lastDown:=high
    lastDownIndex:=bar_index
    lastLow:=low

if(close>open)
    lastUp:=close
    lastUpIndex:=bar_index
    lastUpOpen:=open
    lastUpLow:=low
    lastHigh:=high

lastHigh:=high>lastHigh?high:lastHigh
lastLow:=low<lastLow?low:lastLow
